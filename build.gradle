group 'org.openrndr.guide'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.41'

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.openrndr:dokgen:c59a5934ee'
        classpath "org.ajoberstar:gradle-git-publish:2.0.0"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.openrndr.dokgen-gradle'
apply plugin: 'org.ajoberstar.git-publish'

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

sourceCompatibility = 1.8
project.ext.openrndrVersion = "0.3.35-rc1"

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = "https://dl.bintray.com/openrndr/openrndr/"
    }
    maven { url 'https://jitpack.io' }
}

switch (org.gradle.internal.os.OperatingSystem.current()) {
    case org.gradle.internal.os.OperatingSystem.WINDOWS:
        project.ext.openrndrOS = "windows"
        break
    case org.gradle.internal.os.OperatingSystem.LINUX:
        project.ext.openrndrOS = "linux-x64"
        break
    case org.gradle.internal.os.OperatingSystem.MAC_OS:
        project.ext.openrndrOS = "macos"
        break
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    runtime "org.openrndr:openrndr-gl3:$openrndrVersion"
    runtime "org.openrndr:openrndr-gl3-natives-$openrndrOS:$openrndrVersion"
    compile "org.openrndr:openrndr-core:$openrndrVersion"
    compile "org.openrndr:openrndr-svg:$openrndrVersion"
    compile "org.openrndr:openrndr-animatable:$openrndrVersion"
    compile "org.openrndr:openrndr-extensions:$openrndrVersion"
    compile "org.openrndr:openrndr-filter:$openrndrVersion"
    compile "org.openrndr:openrndr-ffmpeg:$openrndrVersion"
    implementation 'com.github.openrndr:openrndr-panel:v0.3.11'
    implementation 'com.github.openrndr:dokgen:bdc8d110d4'


    compile "com.github.openrndr.orx:orx-noise:v0.0.30"
}

mainClassName = "TemplateProgramKt"

dokgen {
    runner {
        if (System.properties['os.name'] == "Mac OS X") {
            jvmArgs = ["-XstartOnFirstThread"]
        }
    }

    examples {
        webRootUrl = "https://github.com/openrndr/openrndr-examples/blob/master/src/main/kotlin"
    }

    docsify {
        media = [file("$projectDir/media"), file("$projectDir/static-media")]
        assets = [file("$projectDir/data/docsify-assets")]
    }

}

task publishDocs { task ->
    doLast {
        gitPublish.repoUri = "git@github.com:openrndr/openrndr-guide.git"
        gitPublish.branch = "master"
        gitPublish.repoDir = file("$buildDir/gitrepo-docs")
        gitPublish.contents.from("build/dokgen/docsify") {
            exclude "node_modules"
        }
        gitPublish.commitMessage = "Update docs"
    }

    task.group = "dokgen"
    task.finalizedBy(gitPublishPush)
}

task publishExamples { task ->
    doLast {
        gitPublish.repoDir = file("$buildDir/gitrepo-examples")
        gitPublish.contents {
            from "$projectDir/src/main/resources/examples-repo-template"
            from("$projectDir/src/generated/examples") {
                into "src/main/kotlin/examples"
            }
            from("$projectDir/fonts") {
                into "fonts"
            }
        }
        gitPublish.repoUri = "git@github.com:openrndr/openrndr-examples.git"
        gitPublish.branch = "master"
        gitPublish.commitMessage = "Update examples"
    }
    task.group = "dokgen"
    task.finalizedBy(gitPublishPush)
}



ext.mainClassName = mainClassName
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', '**/module-info*'
    archiveName 'myjar.jar'
}

